apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'

// This is the library version used when deploying the artifact
version = '0.6.1'

ext {
    bintrayRepo = 'android'
    bintrayName = 'devicesetup'
    bintrayOrg = 'particle'

    publishedGroupId = 'io.particle'
    libraryName = 'Particle (formerly Spark) Android Device Setup library'
    artifact = 'devicesetup'

    libraryDescription = "The Particle Device Setup library provides everything you need to " +
            "offer your users a simple initial setup process for Particle-powered devices.  This " +
            "includes all the necessary device communication code, an easily customizable UI, and " +
            "a simple developer API."

    siteUrl = 'https://github.com/spark/spark-setup-android'
    gitUrl = 'https://github.com/spark/spark-setup-android.git'

    libraryVersion = project.version

    developerId = 'idok'
    developerName = 'Ido Kleinman'
    developerEmail = 'ido@particle.io'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dexOptions {
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

// TESTING ONLY: to build against a locally built version of the cloud SDK, uncomment these
// lines, and the "compile(name:'cloudsdk', ext:'aar')" line below under dependencies.
// (If you don't know what this means or why we (the SDK maintainers at Particle) would want to
// do this, then you can safely ignore all this and keep it commented out. :)
//repositories {
//    flatDir {
//        dirs 'libs'
//    }
//}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'io.particle:cloudsdk:1.0.0'

    api 'com.squareup.phrase:phrase:1.1.0'
    api 'uk.co.chrisjenx:calligraphy:2.3.0'
    api 'com.segment.analytics.android:analytics:4.3.1'
    api 'com.madgag.spongycastle:core:1.58.0.0'

    api 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'

    api 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'

    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.recyclerview:recyclerview:1.0.0'
    implementation("com.squareup.okio:okio:2.5.0")
    implementation 'com.google.code.gson:gson:2.8.5'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'

    // TESTING ONLY (see other TESTING comments further up)
//    compile(name: 'cloudsdk', ext: 'aar')
}

apply from: '../pom_generator_v1.gradle'
apply from: '../bintray_upload_v1.gradle'

// disable insane, build-breaking doclint tool in Java 8
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        //noinspection SpellCheckingInspection
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
